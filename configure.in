dnl $XTermId: configure.in,v 1.18 2009/10/14 09:47:48 tom Exp $

AC_PREREQ(2.52.20030208)
AC_INIT(luit.c)
AC_CONFIG_HEADER(config.h:config_h.in)
CF_CHECK_CACHE

AC_ARG_PROGRAM

AC_PROG_CC
AC_PROG_CPP
AC_PROG_INSTALL
CF_PROG_LINT

CF_ANSI_CC_REQD
AC_CONST
CF_PROG_EXT

AC_PATH_XTRA
CF_XOPEN_SOURCE
CF_SIGWINCH
CF_SVR4
CF_SYSV
CF_SIZE_T
AC_TYPE_UID_T

CF_DISABLE_ECHO

if test -n "$GCC" ; then
AC_MSG_CHECKING(if you want to turn on gcc warnings)
CF_ARG_ENABLE(warnings,
	[  --enable-warnings       test: turn on GCC compiler warnings],
	[with_warnings=yes],
	[with_warnings=no])
AC_MSG_RESULT($with_warnings)
if test "$with_warnings" = yes
then
	CF_GCC_ATTRIBUTES
	CF_GCC_WARNINGS(Wwrite-strings Wconversion)
fi
fi

AC_CHECK_HEADERS( \ 
poll.h \
pty.h \
stropts.h \
sys/ioctl.h \
sys/param.h \
sys/poll.h \
sys/select.h \
sys/time.h \
termios.h \
) 

AC_CHECK_FUNCS(\
grantpt \
poll \
putenv \
select \
strdup \
)

if test "x$ac_cv_func_grantpt" != "xyes" ; then
	AC_CHECK_LIB(util, openpty, [cf_have_openpty=yes],[cf_have_openpty=no])
	if test "$cf_have_openpty" = yes ; then
		LIBS="-lutil $LIBS"
		AC_DEFINE(HAVE_OPENPTY)
		AC_CHECK_HEADERS( \ 
			util.h \
			libutil.h \
			pty.h \
		)
	fi
fi

CF_SYS_TIME_SELECT 
CF_FUNC_POLL
CF_WITH_ZLIB
CF_X_FONTENC

CF_WITH_LOCALE_ALIAS

AC_OUTPUT(Makefile,,,cat)
