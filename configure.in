dnl $XTermId: configure.in,v 1.34 2012/10/04 09:21:23 tom Exp $
dnl
dnl ---------------------------------------------------------------------------
dnl
dnl Copyright 2006-2010,2011 by Thomas E. Dickey
dnl
dnl                         All Rights Reserved
dnl
dnl Permission to use, copy, modify, and distribute this software and its
dnl documentation for any purpose and without fee is hereby granted,
dnl provided that the above copyright notice appear in all copies and that
dnl both that copyright notice and this permission notice appear in
dnl supporting documentation, and that the name of the above listed
dnl copyright holder(s) not be used in advertising or publicity pertaining
dnl to distribution of the software without specific, written prior
dnl permission.
dnl
dnl THE ABOVE LISTED COPYRIGHT HOLDER(S) DISCLAIM ALL WARRANTIES WITH REGARD
dnl TO THIS SOFTWARE, INCLUDING ALL IMPLIED WARRANTIES OF MERCHANTABILITY
dnl AND FITNESS, IN NO EVENT SHALL THE ABOVE LISTED COPYRIGHT HOLDER(S) BE
dnl LIABLE FOR ANY SPECIAL, INDIRECT OR CONSEQUENTIAL DAMAGES OR ANY DAMAGES
dnl WHATSOEVER RESULTING FROM LOSS OF USE, DATA OR PROFITS, WHETHER IN AN
dnl ACTION OF CONTRACT, NEGLIGENCE OR OTHER TORTIOUS ACTION, ARISING OUT OF
dnl OR IN CONNECTION WITH THE USE OR PERFORMANCE OF THIS SOFTWARE.
dnl
dnl ---------------------------------------------------------------------------

AC_PREREQ(2.52.20030208)
AC_INIT(luit.c)
AC_CONFIG_HEADER(config.h:config_h.in)
CF_CHECK_CACHE

AC_ARG_PROGRAM

CF_PROG_CC
AC_PROG_CPP
AC_PROG_INSTALL
CF_PROG_LINT
CF_MAKE_TAGS

AC_CONST

AC_PATH_XTRA
CF_XOPEN_SOURCE
CF_SIGWINCH
CF_SVR4
CF_SYSV
AC_TYPE_UID_T

CF_DISABLE_ECHO

if test -n "$GCC" ; then
AC_MSG_CHECKING(if you want to turn on gcc warnings)
CF_ARG_ENABLE(warnings,
	[  --enable-warnings       test: turn on GCC compiler warnings],
	[with_warnings=yes],
	[with_warnings=no])
AC_MSG_RESULT($with_warnings)
if test "$with_warnings" = yes
then
	CF_GCC_ATTRIBUTES
	CF_GCC_WARNINGS(Wwrite-strings Wconversion)
fi
fi

AC_CHECK_HEADERS( \ 
poll.h \
pty.h \
stropts.h \
sys/ioctl.h \
sys/param.h \
sys/poll.h \
sys/select.h \
sys/time.h \
termios.h \
) 

AC_CHECK_FUNCS(\
poll \
putenv \
select \
strdup \
)

CF_CHECK_TYPE([nfds_t],[unsigned long],[
#include <sys/types.h>
#ifdef HAVE_SYS_POLL_H
#include <sys/poll.h>
#endif
])

CF_SYS_TIME_SELECT 
CF_FUNC_GRANTPT
CF_FUNC_POLL

AC_MSG_CHECKING(if you want to use fontenc)
CF_ARG_DISABLE(fontenc,
	[  --enable-fontenc         enable/disable use of fontenc],
	[with_fontenc=no],
	[with_fontenc=yes])
AC_MSG_RESULT($with_fontenc)

AC_MSG_CHECKING(if you want to use iconv)
CF_ARG_ENABLE(iconv,
	[  --enable-iconv           enable/disable use of iconv],
	[with_iconv=yes],
	[with_iconv=no])
AC_MSG_RESULT($with_iconv)

if test "$with_iconv" != no || test "$with_fontenc" = no
then
	if test "$with_fontenc" != no
	then
		AC_MSG_WARN(ignoring fontenc since iconv was requested)
	fi
	with_iconv=maybe
elif test "$with_fontenc" != no
then
	CF_WITH_ZLIB
	CF_X_FONTENC
	if test $cf_have_fontenc_libs = yes ; then
		AC_DEFINE(USE_FONTENC,1,[Define to 1 if font-encoding libraries can/should be used])
	else
		with_iconv=maybe
	fi
else
	AC_MSG_WARN(luit requires either fontenc or iconv)
	with_iconv=maybe
fi

AM_LANGINFO_CODESET

# Since iconv is widely available, fallback to using it if fontenc is neither
# requested nor available.
if test "$with_iconv" != no
then
	AM_ICONV
	AC_DEFINE(USE_ICONV,1,[Define to 1 if iconv-libraries should be used])
	CF_ADD_LIBS($LIBICONV)
	EXTRAOBJS="$EXTRAOBJS luitconv\$o"
fi

CF_WITH_LOCALE_ALIAS

CF_DISABLE_LEAKS
CF_ENABLE_TRACE

if test "$with_trace" != no
then
	EXTRAOBJS="$EXTRAOBJS trace\$o"
fi

CF_DISABLE_RPATH_HACK

AC_SUBST(EXTRAOBJS)
AC_OUTPUT(Makefile,,,cat)
